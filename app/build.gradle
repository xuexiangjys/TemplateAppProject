apply plugin: 'com.android.application'
apply plugin: 'com.xuexiang.xaop' //引用xaop插件
apply plugin: 'com.xuexiang.xrouter' //引用xrouter-plugin插件实现自动注册
apply plugin: 'img-optimizer'
//打包时，记得设置true启用
if (isNeedPackage.toBoolean()) {
    apply plugin: 'com.didiglobal.booster'
}

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        applicationId "com.xuexiang.templateproject"
        minSdkVersion 17
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ moduleName : project.getName() ]
            }
        }
    }

    signingConfigs {
        if (isNeedPackage.toBoolean()) {
            release {
                storeFile file(app_release.storeFile)
                storePassword app_release.storePassword
                keyAlias app_release.keyAlias
                keyPassword app_release.keyPassword
            }
        }

        debug {
            storeFile file("./debug.jks")
            storePassword "123456"
            keyAlias "debug"
            keyPassword "123456"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isNeedPackage.toBoolean()) {
                signingConfig signingConfigs.release

                Properties properties = new Properties()
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
                def appID = properties.getProperty("APP_ID_UMENG")
                if (appID != null) {
                    buildConfigField "String", "APP_ID_UMENG", appID
                } else {
                    buildConfigField "String", "APP_ID_UMENG", '""'
                }
            } else {
                signingConfig signingConfigs.debug
                buildConfigField "String", "APP_ID_UMENG", '""'
            }
        }

        debug {
            debuggable true
            minifyEnabled false

            signingConfig signingConfigs.debug
            buildConfigField "String", "APP_ID_UMENG", '""'
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation deps.junit
    androidTestImplementation deps.runner
    androidTestImplementation deps.espresso.core

    implementation deps.androidx.multidex

    //XUI框架
    implementation 'com.github.xuexiangjys:XUI:1.0.9'
    implementation deps.androidx.appcompat
    implementation deps.androidx.recyclerview
    implementation deps.androidx.design
    implementation deps.glide

    //XUtil工具类
    implementation 'com.github.xuexiangjys.XUtil:xutil-core:1.1.6'
    //XAOP切片，版本号前带x的是支持androidx的版本
    implementation 'com.github.xuexiangjys.XAOP:xaop-runtime:1.0.5x'
    //XUpdate版本更新
    implementation 'com.github.xuexiangjys:XUpdate:1.1.1'
    //屏幕适配AutoSize
    implementation 'me.jessyan:autosize:1.1.2'
    //XHttp2
    implementation 'com.github.xuexiangjys:XHttp2:1.0.2'
    implementation deps.rxjava2
    implementation deps.rx_android
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation deps.gson
    //XPage
    implementation 'com.github.xuexiangjys.XPage:xpage-lib:2.2.6'
    annotationProcessor 'com.github.xuexiangjys.XPage:xpage-compiler:2.2.6'
    //页面路由
    implementation 'com.github.xuexiangjys.XRouter:xrouter-runtime:1.0.1'
    annotationProcessor 'com.github.xuexiangjys.XRouter:xrouter-compiler:1.0.1'
    //ButterKnife的sdk
    implementation deps.butterknife.runtime
    annotationProcessor deps.butterknife.compiler
    //如果开启了内存泄漏监测leak,就需要加上这个依赖
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.3'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'

    //umeng统计
    implementation 'com.umeng.umsdk:analytics:8.0.2'
    implementation 'com.umeng.umsdk:common:2.0.2'

    //美团多渠道打包
    implementation 'com.meituan.android.walle:library:1.1.6'
}

//walle多渠道打包
apply from: 'multiple-channel.gradle'

